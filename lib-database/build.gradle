/*
 * Note: If the Android plugin is enabled, since Gradle 5.4, the annotation processor will not run
 * with kapt (using project or files to locate the annotation processor).
 */

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$version_kotlin"
    }
}

apply plugin: "kotlin-multiplatform"
apply plugin: "maven-publish"
apply plugin: "jacoco"
apply plugin: "kotlin-kapt"

kotlin {

    targets {
        fromPreset(presets.jvm, 'jvm')
        fromPreset(presets.js, 'js')  {
            compilations.main.kotlinOptions {
                metaInfo = true
                sourceMap = true
                moduleKind = "commonjs"
                outputFile = "${rootProject.buildDir}/npm/${project.name}/${project.name}.js"
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common:$version_kotlin"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:$version_kotlinx_serialization"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$version_coroutines"
                implementation project(":lib-util")
                implementation project(":lib-database-entities")
                implementation project(':lib-door-runtime')
                implementation project(":lib-database-annotations")
                compileOnly project(":lib-room-annotations")

            }
        }

        jvmMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$version_kotlin"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$version_kotlinx_serialization"
            }
        }
        jsMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$version_kotlin"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$version_kotlinx_serialization"
            }
        }

    }

}


// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}


kapt {
    arguments {
        arg("door_jdbc_kt_out", rootProject.file("lib-database-mpp/build/generated/source/door").absolutePath)
    }
}


dependencies {
    /**
     * Since Gradle 5.4 using kapt project(":...") does not work.
     */
    kapt files("../lib-database-annotation-processor/build/libs/lib-database-annotation-processor.jar",
            "../lib-door-runtime/build/libs/lib-door-runtime-jvm.jar")
    kapt "org.xerial:sqlite-jdbc:$version_sqlite_jdbc"
    kapt "com.squareup:kotlinpoet:1.2.0"
    kapt "com.github.jsqlparser:jsqlparser:$version_jsqlparser"
    kapt "org.apache.commons:commons-text:$version_apache_commons_text"
}

compileKotlinJvm.dependsOn(":lib-database-annotation-processor:jar")

publishing {
    publications {
        maven(MavenPublication) {
            groupId rootProject.group
            artifactId project.name
            version rootProject.version
        }
    }

    repositories {
        maven {
            url rootProject.ext.buildConfigProperties['repo.dir']
        }
    }
}