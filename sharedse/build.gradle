apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: "maven-publish"

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

repositories {
    mavenCentral()
    maven {
        url "http://devserver2.ustadmobile.com/repo-mvn"
    }

}

/*
 * Added to overcome limitation on Android studio as per:
 *  http://tools.android.com/knownissues#TOC-JUnit-tests-missing-resources-in-classpath-when-run-from-Studio
 */
task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    from "${projectDir}/../core-tests/src/main/resources"
    from "${projectDir}/../sharedse-tests/src/main/resources"
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

dependencies {
    //compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'org.nanohttpd:nanohttpd:2.3.1'
    compile 'org.nanohttpd:nanohttpd-nanolets:2.3.1';
    compile 'com.ustadmobile.qrcodelibomrfork:qrcode-core:1.0.3'
    compile 'com.squareup.okhttp3:okhttp:3.9.0'
    project.ext.nanoLrsVersion = rootProject.ext.nanoLrsVersion
    compile "com.ustadmobile.nanolrs:nanolrs-core:$nanoLrsVersion"
    compile "com.ustadmobile.nanolrs:nanolrs-http:$nanoLrsVersion"
    compile project(":core")
    compile project(':lib-core-fs')
    testCompile project(":sharedse-tests")
    testCompile project(":core-tests")
}

publishing {
    publications {
        nanolrscore(MavenPublication) {
            groupId 'com.ustadmobile.app'
            artifactId 'sharedse'
            version rootProject.version

            from components.java

            artifact sourceJar {
                classifier = 'sources'
            }
        }
    }

    repositories {
        maven {
            url rootProject.ext.buildConfigProperties['repo.dir']
        }
    }
}
