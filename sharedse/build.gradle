apply plugin: 'java-library'
apply plugin: 'jacoco'
apply plugin: "maven-publish"

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

repositories {
    mavenCentral()
    maven {
        url "http://devserver2.ustadmobile.com/repo-mvn"
    }

}

/*
 * Added to overcome limitation on Android studio as per:
 *  http://tools.android.com/knownissues#TOC-JUnit-tests-missing-resources-in-classpath-when-run-from-Studio
 */
task copyTestResources(type: Copy) {
    from "${projectDir}/src/test/resources"
    from "${projectDir}/../core-tests/src/main/resources"
    from "${projectDir}/../sharedse-tests/src/main/resources"
    into "${buildDir}/classes/test"
}
processTestResources.dependsOn copyTestResources

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

dependencies {
    //compile fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    implementation 'org.nanohttpd:nanohttpd-nanolets:2.3.1';
    implementation 'com.ustadmobile.qrcodelibomrfork:qrcode-core:1.0.3'
    implementation 'com.squareup.okhttp3:okhttp:3.9.0'
    implementation "net.sf.kxml:kxml2:2.3.0";
    implementation "com.google.code.gson:gson:2.8.2"
    implementation "net.lingala.zip4j:zip4j:1.3.2"

    project.ext.nanoLrsVersion = rootProject.ext.nanoLrsVersion
    implementation "com.ustadmobile.nanolrs:nanolrs-core:$nanoLrsVersion"
    implementation "com.ustadmobile.nanolrs:nanolrs-http:$nanoLrsVersion"
    implementation project(":core")
    implementation project(":lib-database")
    implementation project(':lib-core-fs')
    implementation project(":lib-util")
    testImplementation project(":sharedse-tests")
    testImplementation project(":core-tests")
}

publishing {
    publications {
        nanolrscore(MavenPublication) {
            groupId 'com.ustadmobile.app'
            artifactId 'sharedse'
            version rootProject.version

            from components.java

            artifact sourceJar {
                classifier = 'sources'
            }
        }
    }

    repositories {
        maven {
            url rootProject.ext.buildConfigProperties['repo.dir']
        }
    }
}
