plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.multiplatform'
}

android {
    compileSdkVersion rootProject.ext.version_android_compile_sdk
    buildToolsVersion rootProject.ext.version_android_buildtools

    defaultConfig {
        minSdkVersion rootProject.ext.version_android_min_sdk
        targetSdkVersion rootProject.ext.version_android_target_sdk
    }
}

apply plugin: "maven-publish"


kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm')
        //Uncomment this for autocomplete etc. to work. Comment it out before compiling the rest of
        // the project to avoid it being seen as an extra jvm variant
        //fromPreset(presets.jvm, 'commonJvm') //comment this out for code completion
        fromPreset(presets.js, 'js') {
            compilations.main.kotlinOptions {
                metaInfo = true
                sourceMap = true
                moduleKind = "commonjs"
                outputFile = "${rootProject.buildDir}/npm/${project.name}/${project.name}.js"
            }
        }
        fromPreset(presets.android, 'android')
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin("stdlib-common:$version_kotlin")
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$version_kotlinx_serialization"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$version_coroutines"
                implementation "com.ustadmobile.kmpxmlpullparser:kmpxmlpullparser-metadata:$version_kmp"
                implementation project(":lib-door-runtime")
            }
        }
        jsMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-js:$version_kotlin"
                implementation "com.ustadmobile.kmpxmlpullparser:kmpxmlpullparser-js:$version_kmp"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:$version_kotlinx_serialization"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$version_coroutines"
            }
        }
        commonJvmMain {
            dependencies {
                implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$version_kotlin"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$version_kotlinx_serialization"
                implementation "android.arch.lifecycle:extensions:$version_android_lifecycle"
            }
        }

        jvmMain {
            dependencies {
                implementation "com.ustadmobile.kmpxmlpullparser:kmpxmlpullparser-jvm:$version_kmp"
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$version_coroutines"
            }
            dependsOn commonJvmMain
        }

        androidMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$version_coroutines"
                implementation "com.ustadmobile.kmpxmlpullparser:kmpxmlpullparser-jvm:$version_kmp"
                implementation "androidx.lifecycle:lifecycle-extensions:$version_androidx_lifecycle"
                implementation "androidx.room:room-runtime:$version_android_room"
            }
            dependsOn commonJvmMain
        }


    }
}


// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId rootProject.group
            artifactId project.name
            version rootProject.version
        }
    }

    repositories {
        maven {
            url rootProject.ext.buildConfigProperties['repo.dir']
        }
    }
}