apply plugin: 'java-library'
apply plugin: "maven-publish"

compileTestJava {
    /*
     *  Annotation processor arguments:
     *   umdb_room_out - Destination directory for the generated room version of database and DAO classes
     */
    options.compilerArgs += [
            "-Aumdb_jdbc_out=" + project.file("build/generated/sources/test-umdbprocessor").getAbsolutePath(),
            "-Aumdb_no_default_factory=true"
    ]
}

sourceSets {
    test {
        java {
            srcDirs += ["build/generated/sources/test-umdbprocessor"]
        }
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(":lib-database-annotation")
    implementation project(":lib-database-jdbc")
    implementation "com.squareup:javapoet:$version_javapoet"
    implementation "com.github.h-thurow:simple-jndi:$version_simple_jndi"
    implementation "org.xerial:sqlite-jdbc:$version_sqlite_jdbc"

    testAnnotationProcessor project(":lib-database-annotation-processor-core")

    testImplementation "junit:junit:$version_junit"
}

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

publishing {
    publications {
        nanolrscore(MavenPublication) {
            groupId 'com.ustadmobile.app'
            artifactId 'lib-database-annotation-processor-core'
            version rootProject.version

            from components.java

            artifact sourceJar {
                classifier = 'sources'
            }
        }
    }

    repositories {
        maven {
            url rootProject.ext.buildConfigProperties['repo.dir']
        }
    }
}

