/**
 * Core module build script : see buildconfig.core.default.gradle for configuration
 */

buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'gradle.plugin.de.fuerstenau:BuildConfigPlugin:1.1.7'
    }
}

plugins {
    id "java"
}

sourceCompatibility = "1.7"
targetCompatibility = "1.7"


repositories {
    mavenCentral()
    maven {
        url "http://devserver2.ustadmobile.com/repo-mvn"
    }
}

dependencies {
    compile "org.json:json:20160212";
    compile "net.sf.kxml:kxml2:2.3.0";
    compile 'com.ustadmobile.qrcodelibomrfork:qrcode-core:1.0-SNAPSHOT'
    compile 'com.ustadmobile.nanolrs:nanolrs-core:0.1.37'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

ext.buildConfigProperties = new Properties()
ext.buildConfigProperties.load(new FileInputStream(rootProject.file("buildconfig.core.default.properties")))
if(rootProject.file("buildconfig.core.local.properties").exists()) {
    ext.buildConfigProperties.load(new FileInputStream(rootProject.file("buildconfig.core.local.properties")))
}

apply from: rootProject.file("buildconfig.core.gradle").exists() ?
        rootProject.file("buildconfig.core.gradle") : rootProject.file("buildconfig.core.default.gradle")

compileJava.dependsOn(generateCoreBuildConfig)

/**
 * Generate build constants for testing purposes (e.g. remote servers etc.
 */

task generateCoreTestConstantsBuildConfig (type: de.fuerstenau.gradle.buildconfig.GenerateBuildConfigTask) {
    outputDir = new File("${projectDir}/src/test/java")
    packageName ="com.ustadmobile.test.core.buildconfig"
    clsName = "TestConstants"
    buildConfigField "String", "LOGIN_USER", buildConfigProperties["test.remote_user"]
    buildConfigField "String", "LOGIN_PASS", buildConfigProperties["test.remote_auth"]
    buildConfigField "String", "XAPI_SERVER", buildConfigProperties["test.remote_xapi_server"]
    buildConfigField "String", "LOGIN_URL", buildConfigProperties["test.remote_login_url"]
    buildConfigField "String", "TEST_SERVER", buildConfigProperties["test.remote_http_test_server"]
    buildConfigField "String", "TEST_CONTROL_PORT", buildConfigProperties["test.remote_http_control_port"]
    buildConfigField "String", "CATALOG_OPDS_ROOT", buildConfigProperties["test.catalog_opds_root"]
    buildConfigField "int", "DEFAULT_NETWORK_INTERVAL", buildConfigProperties["test.remote_network_interval"]
    buildConfigField "int", "DEFAULT_NETWORK_TIMEOUT", buildConfigProperties["test.remote_network_timeout"]
    buildConfigField "String", "REGISTER_URL", buildConfigProperties["test.register_url"]
}
compileTestJava.dependsOn(generateCoreTestConstantsBuildConfig)
