
plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.multiplatform'
}

android {
    compileSdkVersion rootProject.ext.version_android_compile_sdk
    buildToolsVersion rootProject.ext.version_android_buildtools

    defaultConfig {
        minSdkVersion rootProject.ext.version_android_min_sdk
        targetSdkVersion rootProject.ext.version_android_target_sdk
    }
}

kotlin {
    targets {
        fromPreset(presets.jvm, 'jvm')
        fromPreset(presets.js, 'js')  {
            compilations.main.kotlinOptions {
                metaInfo = true
                sourceMap = true
                moduleKind = "commonjs"
                outputFile = "${rootProject.buildDir}/npm/${project.name}/${project.name}.js"
            }
        }
        fromPreset(presets.android, 'android')
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin("stdlib-common:$version_kotlin")
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$version_coroutines"
            }
        }

        jvmMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$version_coroutines"
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
            }
        }

        jsMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-js:$version_coroutines"
            }
        }

        androidMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$version_coroutines"
                implementation "androidx.room:room-runtime:$version_android_room"
                implementation "androidx.lifecycle:lifecycle-extensions:$version_androidx_lifecycle"
            }
        }
    }
}

//Kotlin plugin applied using subprojects DSL on the root project as advised by documentation
apply plugin: "maven-publish"


publishing {
    publications {
        maven(MavenPublication) {
            groupId rootProject.group
            artifactId project.name
            version rootProject.version
        }
    }

    repositories {
        maven {
            url rootProject.ext.buildConfigProperties['repo.dir']
        }
    }
}