apply plugin: 'com.android.application'
apply plugin: "jacoco"

ext.buildConfigProperties = new Properties()
ext.buildConfigProperties.load(new FileInputStream(project.file("../app-android/buildconfig.default.properties")))
if (project.file("../app-android/buildconfig.local.properties").exists()) {
    ext.buildConfigProperties.load(new FileInputStream(project.file("../app-android/buildconfig.local.properties")))
}

//See:
// https://developer.android.com/studio/projects/android-library.html

//As Per https://developer.android.com/studio/publish/app-signing.html#release-mode
def keyStorePropertiesExists = rootProject.file("keystore.properties").exists()
def keystoreProperties = new Properties()

if (keyStorePropertiesExists) {
    keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))
}


android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    lintOptions {
        abortOnError false
    }

    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.toughra.ustadmobile.launcher"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        manifestPlaceholders = [applicationClass : "android.app.Application"]

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    signingConfigs {
        release {
            keyAlias keyStorePropertiesExists ? keystoreProperties['keyAlias'] : "default"
            keyPassword keyStorePropertiesExists ? keystoreProperties['keyPassword'] : "default"
            storeFile keyStorePropertiesExists ? file(keystoreProperties['storeFile']) : new File("keystore.ks")
            storePassword keyStorePropertiesExists ? keystoreProperties['storePassword'] : "default"
        }
    }

    dexOptions {
        jumboMode = true
    }

    flavorDimensions "config"

    productFlavors {
        /*
         * The localconfig flavor is created based on the buildconfig.local.properties settings
         */
        localconfig {
            dimension "config"
            applicationId project.ext.buildConfigProperties['android.applicationId']
            testApplicationId = project.ext.buildConfigProperties['android.applicationId'] + ".test"
            versionCode rootProject.ext.ustadVersionCode + Integer.parseInt(rootProject.ext.buildConfigProperties['localVersionCode'])
        }

        /*
         * The vanilla config flavor is here for convenience : it's the same as having no
         * buildconfig.local.properties and relying on what's in buildconfig.default.properties
         *
         * It's just here to make life easier so you can switch flavors in Android studio without
         * having to remove the local config files themselves
         */
        vanilla {
            dimension "config"
            applicationId "com.toughra.ustadmobile"
            testApplicationId "com.toughra.ustadmobile.appandroid.test"
            versionCode rootProject.ext.ustadVersionCode
        }

        vanillamultidex {
            dimension "config"
            applicationId "com.toughra.ustadmobile"
            testApplicationId "com.toughra.ustadmobile.appandroid.test"
            versionCode rootProject.ext.ustadVersionCode
            multiDexEnabled true
            packagingOptions { //Required: otherwise we get complaints on assembling
                merge "META-INF/INDEX.LIST"
            }
        }
    }

    buildTypes {
        debug {
            testCoverageEnabled = true
        }

        release {
            testCoverageEnabled = false
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

configurations.all {
    resolutionStrategy {
        force 'com.android.support:appcompat-v7:27.0.2'
        force 'com.android.support:cardview-v7:27.0.2'
        force 'com.android.support:design:27.0.2'
        force 'com.android.support:support-v4:27.0.2'
    }

    exclude group: "net.sf.kxml", module: "kxml2"
    exclude group: "org.json", module: "json"
    exclude module: "lib-database"

}


dependencies {
    api project(':core')
    api project(':sharedse')

    api project(":app-android")

    vanillamultidexApi "com.android.support:multidex:1.0.2"


    api fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

//    api 'com.android.support:multidex:1.0.2'
    testImplementation 'junit:junit:4.12'
}

